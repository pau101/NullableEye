buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
	}
}
apply plugin: "net.minecraftforge.gradle.forge"

ext.modVersion = "1.0.0"
ext.loadingPlugin = "com.pau101.nullableeye.asm.NullableEyeLoadingPlugin"
ext.mappingsChannel = "snapshot"
ext.mappingsVersion = "20161220"
ext.modID = "nullableeye"
version = "$modVersion-1.11.2"
group = "com.pau101.$modID"
archivesBaseName = "$modID"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = "1.11.2-13.20.0.2309"
	runDir = "run"
	mappings = "${project.mappingsChannel}_${project.mappingsVersion}"
	makeObfSourceJar = false
	clientJvmArgs = [ "-Dfml.coreMods.load=$project.loadingPlugin" ]
	serverJvmArgs = [ "-Dfml.coreMods.load=$project.loadingPlugin" ]
}

jar {
	manifest {
		attributes "FMLCorePlugin": "$project.loadingPlugin"
		attributes "FMLCorePluginContainsFMLMod": "true"
	}
}

processResources {
	inputs.property "version", project.modVersion
	inputs.property "mcversion", project.minecraft.version
	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.modVersion, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

task generateMappings(type: Copy) {
	from ("${System.properties["user.home"]}/.gradle/caches/minecraft/de/oceanlabs/key/mcp_$project.mappingsChannel/$project.mappingsVersion/$project.minecraft.version/srgs") {
		include "srg-key.srg"
	}
	into "src/main/resources/assets/$project.modID"
}

compileJava.dependsOn generateMappings
decompileMc.dependsOn generateMappings
